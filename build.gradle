buildscript {
    ext.cubaVersion = '7.1.1'
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://dl.bintray.com/cuba-platform/main'
        }
        jcenter()
    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1+'
    }
}

def modulePrefix = 'cuba-jpa-repositories'

def artifactGlobalName = modulePrefix + "-global"
def artifactCoreName = modulePrefix + "-core"

def globalModule = project(artifactGlobalName)
def coreModule = project(artifactCoreName)

def servletApi = 'javax.servlet:javax.servlet-api:3.1.0'

apply(plugin: 'idea')
apply(plugin: 'cuba')

def artifactGroup = 'com.haulmont.addons.cuba.jpa.repositories'
def artifactVersion = '0.1.2'
def isArtifactSnapshot = false

cuba {
    artifact {
        group = artifactGroup
        version = artifactVersion
        isSnapshot = isArtifactSnapshot
    }
    tomcat {
        dir = "$project.rootDir/deploy/tomcat"
    }
}

dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")
}

configure([globalModule, coreModule]) {
    apply(plugin: 'idea')
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'maven-publish')
    apply(plugin: 'cuba')
    apply(plugin: 'com.jfrog.bintray')

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadocJar.dependsOn javadoc

    artifacts {
        archives sourceJar
        archives javadocJar
    }
}

configure(globalModule) {
    apply(plugin: 'maven')
    apply(plugin: 'maven-publish')


    dependencies {
        if (!JavaVersion.current().isJava8()) {
            runtime('javax.xml.bind:jaxb-api:2.3.1')
            runtime('org.glassfish.jaxb:jaxb-runtime:2.3.1')
        }
        compile(group: 'org.springframework.data', name: 'spring-data-commons', version: '1.13.13.RELEASE') {
            exclude(group: 'org.hibernate', module: 'hibernate-core')
            exclude(group: 'org.springframework', module: 'spring-context')
            exclude(group: 'org.springframework', module: 'spring-core')
            exclude(group: 'org.springframework', module: 'spring-beans')
            exclude(group: 'org.springframework', module: 'spring-tx')
            exclude(group: 'org.springframework', module: 'spring-orm')
            exclude(group: 'org.springframework', module: 'spring-aop')
            exclude(group: 'org.springframework', module: 'spring-expression')
        }
    }

    jar {
        manifest {
            attributes('App-Component-Id': cuba.artifact.group)
            attributes('App-Component-Version': cuba.artifact.version + (cuba.artifact.isSnapshot ? '-SNAPSHOT' : ''))
        }
    }

    publishing {
        publications {
            publishGlobal(MavenPublication) {
                groupId = artifactGroup
                artifactId = artifactGlobalName
                version = artifactVersion + (cuba.artifact.isSnapshot ? '-SNAPSHOT' : '')
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
    }

    bintray {
        publications = ['publishGlobal']
    }

}

configure(coreModule) {

    def hsql = 'org.hsqldb:hsqldb:2.4.1'

    configurations {
        jdbc
        dbscripts
    }

    dependencies {
        compile(globalModule)
        compileOnly(servletApi)
        jdbc(hsql)
        testRuntime(hsql)
        testCompile('junit:junit:4.12')
    }

    entitiesEnhancing {
        test {
            enabled = true
        }
    }

    task cleanConf(description: 'Cleans up conf directory') {
        doLast {
            def dir = new File(cuba.tomcat.dir, "/conf/${modulePrefix}-core")
            if (dir.isDirectory()) {
                ant.delete(includeemptydirs: true) {
                    fileset(dir: dir, includes: '**/*', excludes: 'local.app.properties')
                }
            }
        }
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}-core"
        appJars(modulePrefix + '-global', modulePrefix + '-core')
    }

    task startDb(type: CubaHsqlStart) {
        dbName = 'cubajparepositories'
    }

    task createDb(dependsOn: assembleDbScripts, description: 'Creates local database', type: CubaDbCreation) {
        dbms = 'hsql'
        host = 'localhost'
        dbName = 'cubajparepositories'
        dbUser = 'sa'
        dbPassword = ''
    }

    task updateDb(dependsOn: assembleDbScripts, description: 'Updates local database', type: CubaDbUpdate) {
        dbms = 'hsql'
        host = 'localhost'
        dbName = 'cubajparepositories'
        dbUser = 'sa'
        dbPassword = ''
    }
}

task undeploy(type: Delete) {
    delete("$cuba.tomcat.dir/shared")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}-core")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}")
}

task restart(dependsOn: ['stop', ":${modulePrefix}-core:deploy", ":${modulePrefix}-core:deploy"], description: 'Redeploys applications and restarts local Tomcat') {
    doLast {
        ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
            not {
                socket(server: 'localhost', port: '8787')
            }
        }
        start.execute()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

subprojects {
    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        // make files public ?
        publish = true
        // override existing artifacts?
        override = true

        // metadata
        pkg {
            repo = 'labs'           // your repository name
            name = 'cuba-jpa-repositories'  // package name - it will be created upon upload
            userOrg = 'cuba-platform'
            licenses = ["Apache-2.0"]
            websiteUrl = 'https://github.com/cuba-rnd/cuba-jpa-repositories'
            issueTrackerUrl = 'https://github.com/cuba-rnd/cuba-jpa-repositories/issues'
            vcsUrl = 'https://github.com/cuba-rnd/cuba-jpa-repositories.git' // Mandatory for Open Source projects
            labels = ['cuba-platform', 'opensource']
            publicDownloadNumbers = true
        }
    }
}
